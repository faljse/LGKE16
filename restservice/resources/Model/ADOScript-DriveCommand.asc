CC "Modeling" GET_ACT_MODEL
SET actModelId:(modelid)

CC "Core" GET_CLASS_ID classname:("DriveCommand")
SET driveCommandClassId:(classid)
CC "Core" GET_ATTR_ID classid:(driveCommandClassId) attrname:"Direction"
SET directionAttrId:(attrid)
CC "Core" GET_ATTR_ID classid:(driveCommandClassId) attrname:"Speed"
SET speedAttrId:(attrid)
CC "Core" GET_ATTR_ID classid:(driveCommandClassId) attrname:"_active"
SET activeAttrId:(attrid)
CC "Core" GET_CLASS_ID classname:("Duration")
SET durationClassId:(classid)
CC "Core" GET_ATTR_ID classid:(durationClassId) attrname:"Duration"
SET durationAttrId:(attrid)

CC "Core" GET_CLASS_ID classname:("Start")
SET startClassId:(classid)
CC "Core" GET_ATTR_ID classid:(startClassId) attrname:"tempResultFile"
SET tempResultFileAttrId:(attrid)
CC "Core" GET_ATTR_ID classid:(startClassId) attrname:"POST"
SET postAttrId:(attrid)
CC "Core" GET_ATTR_ID classid:(startClassId) attrname:"GET"
SET getAttrId:(attrid)
CC "Core" GET_ATTR_ID classid:(startClassId) attrname:"Endpoint"
SET endpointAttrId:(attrid)
CC "Core" GET_ATTR_ID classid:(startClassId) attrname:"Parameter"
SET parameterAttrId:(attrid)

# there should be only one start and stop object
CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"Start"
SET startObjId:(VAL objids)
CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"Stop"
SET stopObjId:(VAL objids)

CC "Core" GET_ATTR_VAL objid:(startObjId) attrid:(postAttrId)
SET callPost:(val)
CC "Core" GET_ATTR_VAL objid:(startObjId) attrid:(getAttrId)
SET callGet:(val)
CC "Core" GET_ATTR_VAL objid:(startObjId) attrid:(endpointAttrId)
SET callEndpoint:(val)
CC "Core" GET_ATTR_VAL objid:(startObjId) attrid:(parameterAttrId)
SET callParameter:(val)
CC "Core" GET_ATTR_VAL objid:(startObjId) attrid:(tempResultFileAttrId)
SET callTempResultFile:(val)

# start should only have 1 connector
CC "Core" GET_CONNECTORS objid:(startObjId) out
SET startingPointConnId:(VAL objids)

#retrieve the toobj which should be the first driveCommand
CC "Core" GET_CONNECTOR_ENDPOINTS objid:(startingPointConnId)
SET nextCommandObjId:(toobjid)

WHILE(nextCommandObjId != -1) {
	CC "Core" SET_ATTR_VAL objid:(nextCommandObjId) attrid:(activeAttrId) val:0
	
	CC "Core" GET_CONNECTORS objid:(nextCommandObjId) out
	SET nextConnId:(VAL objids)
	
	CC "Core" GET_CONNECTOR_ENDPOINTS objid:(nextConnId)
	SET nextCommandObjId:(toobjid)
	
	IF (nextCommandObjId = stopObjId) {
		SET nextCommandObjId:(-1)
	}
}

#retrieve the toobj which should be the first driveCommand
CC "Core" GET_CONNECTOR_ENDPOINTS objid:(startingPointConnId)
SET nextCommandObjId:(toobjid)

WHILE(nextCommandObjId != -1) {
	CC "Core" SET_ATTR_VAL objid:(nextCommandObjId) attrid:(activeAttrId) val:1

	#now get the attribute values
	CC "Core" GET_ATTR_VAL objid:(nextCommandObjId) attrid:(directionAttrId)
	SET direction:(val)
	CC "Core" GET_ATTR_VAL objid:(nextCommandObjId) attrid:(speedAttrId)
	SET speed:(STR(val))

	# there should be only one outgoing connection
	CC "Core" GET_CONNECTORS objid:(nextCommandObjId) out
	SET nextConnId:(VAL objids)
	CC "Core" GET_ATTR_VAL objid:(nextConnId) attrid:(durationAttrId)
	SET duration:(val)
	
	#Build and Execute Command
	IF (direction = "Forward") {
		SET callParam:(speed+",FORWARD,"+speed+",FORWARD,"+speed+",FORWARD,"+speed+",FORWARD")
	}
	ELSIF (direction = "BACKWARD") {
		SET callParam:(speed+",BACKWARD,"+speed+",BACKWARD,"+speed+",BACKWARD,"+speed+",BACKWARD")
	}
	ELSIF (direction = "LeftRot") {
		SET callParam:(speed+",BACKWARD,"+speed+",FORWARD,"+speed+",BACKWARD,"+speed+",FORWARD")
	}
	ELSIF (direction = "RightRot") {
		SET callParam:(speed+",FORWARD,"+speed+",BACKWARD,"+speed+",FORWARD,"+speed+",BACKWARD")
	}
	ELSIF (direction = "Break") {
		SET callParam:(speed+",STOP,"+speed+",STOP,"+speed+",STOP,"+speed+",STOP")
	}
	SET callParam:("\""+callParameter+"="+callParam+"\"")
	#CC "AdoScript" INFOBOX (callPost+" "+callEndpoint+" "+callParam+" "+callTempResultFile)
	SYSTEM (callPost+" "+callEndpoint+" "+callParam+" "+callTempResultFile)
	#CC "AdoScript" FREAD file:(callTempResultFile) binary:0 base64:0
	#SET callResult:(text)
	
	CC "AdoScript" SLEEP ms:(duration)
	
	CC "Core" SET_ATTR_VAL objid:(nextCommandObjId) attrid:(activeAttrId) val:0
	CC "Core" GET_CONNECTOR_ENDPOINTS objid:(nextConnId)
	SET nextCommandObjId:(toobjid)
	
	IF (nextCommandObjId = stopObjId) {
		SET nextCommandObjId:(-1)
	}
}

#retrieve the toobj which should be the first driveCommand
CC "Core" GET_CONNECTOR_ENDPOINTS objid:(startingPointConnId)
SET nextCommandObjId:(toobjid)

WHILE(nextCommandObjId != -1) {
	CC "Core" SET_ATTR_VAL objid:(nextCommandObjId) attrid:(activeAttrId) val:1
	
	CC "Core" GET_CONNECTORS objid:(nextCommandObjId) out
	SET nextConnId:(VAL objids)
	
	CC "Core" GET_CONNECTOR_ENDPOINTS objid:(nextConnId)
	SET nextCommandObjId:(toobjid)
	
	IF (nextCommandObjId = stopObjId) {
		SET nextCommandObjId:(-1)
	}
}
